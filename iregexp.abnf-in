i-regexp = branch *( "|" branch )
branch = *piece
piece = atom [quantifier]
quantifier = <<?*+>> / ( "{" quantity "}" )
quantity = QuantExact [ "," [ QuantExact ] ]
QuantExact = <<0-9>>+
atom = NormalChar / charClass / ( "(" i-regexp ")" )
NormalChar = <<^.\?*+{}()|[\\]>>

charClass = "." / SingleCharEsc / charClassEsc / charClassExpr
SingleCharEsc = "\" <<nrt\|.?*+(){}\-[\\]\^>>
charClassEsc = catEsc / complEsc
charClassExpr = "[" ["^"] ("-" / CCE1) *CCE1 ["-"] "]"
CCE1 = (CCchar ["-" CCchar]) / charClassEsc
CCchar = <<^-[]\\>> / SingleCharEsc

catEsc = "\p{" charProp "}"
complEsc = "\P{" charProp "}"
charProp = IsCategory
IsCategory = Letters / Marks / Numbers / Punctuation / Separators / Symbols / Others
Letters = "L" <<ultmo>>?
Marks = "M" <<nce>>?
Numbers = "N" <<dlo>>?
Punctuation = "P" <<cdseifo>>?
Separators = "Z" <<slp>>?
Symbols = "S" <<mcko>>?
Others = "C" <<cfon>>?
